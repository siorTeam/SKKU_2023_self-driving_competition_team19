//주석 및 디버그 추가
#include <PID_v1.h>

// Pin 설정
const int potentiometerPin = A8;  // 가변저항 연결 핀
int motor_f_1 = 2;
int motor_f_2 = 3;

// PID 설정
// 앞바퀴
double setpoint;  // 목표값(튜닝 완료)
double pid_input, pid_output;     // PID 제어에 사용할 변수
double consKp = 4.0;  // 보수적인 제어에 사용할 하이퍼 파라미터
double consKi = 0.011;
double consKd = 0.125;
double aggKp = 10;  // 공격적인 제어에 사용할 하이퍼 파라미터
double aggKi = 0.0075;
double aggKd = 0.6;
double chng_max = 20; // (튜닝 필요)

// 폭에 의한 제어값 변화량 (튜닝 필요)
double W_setpoint = 100.0;  // 이상적인 차폭
double W_pid_input, W_chng_output;     // 차선과의 폭 PID 제어에 사용할 변수
double W_consKp = 1;  // 보수적인 제어에 사용할 하이퍼 파라미터: 멀리 있을 때
double W_consKi = 0.01;
double W_consKd = 0.1;
double W_aggKp = 1;  // 공격적인 제어에 사용할 하이퍼 파라미터: 가까이 있을 때
double W_aggKi = 0.01;
double W_aggKd = 0.1;
// 폭에 의한 제어값 변화량 (튜닝 필요)
double A_setpoint = 0.0;  // 이상적인 차폭
double A_pid_input, A_chng_output;     // 차선과의 폭 PID 제어에 사용할 변수
double A_consKp = 1.0;  // 보수적인 제어에 사용할 하이퍼 파라미터: 멀리 있을 때
double A_consKi = 0.01;
double A_consKd = 0.1;
double A_aggKp = 1;  // 공격적인 제어에 사용할 하이퍼 파라미터: 가까이 있을 때
double A_aggKi = 0.01;
double A_aggKd = 0.1;

double CarLine_W = 100; // 테스트 시 초기값 설정
double CarLine_A = 0; // 테스트 시 초기값 설정
double chng_sum;
double N_setpoint = 570;
double CarLine_W_offset = 100;
double CarLine_A_offset = 0;

// PID 객체 생성
PID pid(&pid_input, &pid_output, &setpoint, consKp, consKi, consKd, DIRECT);  // 핸들 제어 PID 객체 생성
PID W_pid(&W_pid_input, &W_chng_output, &W_setpoint, W_consKp, W_consKi, W_consKd, DIRECT);  // 차선과 폭 제어 PID 객체 생성
PID A_pid(&A_pid_input, &A_chng_output, &A_setpoint, A_consKp, A_consKi, A_consKd, DIRECT);  // 차선과 각도 PID 객체 생성


void setup() {
  // DC 모터 설정
  pinMode(motor_f_1, OUTPUT);
  pinMode(motor_f_2, OUTPUT);
  Serial.begin(9600);
  
  // PID 제어 설정
  pid.SetMode(AUTOMATIC);
  pid.SetOutputLimits(-255, 255);  // 출력 제한 설정
  W_pid.SetMode(AUTOMATIC);
  W_pid.SetOutputLimits(-20, 20);  // 출력 제한 설정
  A_pid.SetMode(AUTOMATIC);
  A_pid.SetOutputLimits(-20, 20);  // 출력 제한 설정
}

void loop() {
  int potentiometerValue = analogRead(potentiometerPin);  // 가변저항 값 읽기

  // 폭 차에 의한 PID 제어
  W_pid_input = CarLine_W; // +가 왼쪽으로 간 상황(우회전 필요)
  if (abs(CarLine_W - CarLine_W_offset) < 8) {  // 폭 목표값과의 오차 계산 (튜닝 필요)
    W_pid.SetTunings(W_consKp, W_consKi, W_consKd); // 폭이 offset과 가까우면 보수적으로
  } else { // 각이 offset과 멀면 보수적으로
    W_pid.SetTunings(W_aggKp, W_aggKi, W_aggKd);
  }
  W_pid.Compute();  // PID 제어 계산 -> W_chng_output +가 왼쪽으로 간 상황(우회전 필요)

  // 각도 차에 의한 PID 제어
  A_pid_input = CarLine_A; // +가 왼쪽으로 간 상황(우회전 필요)
  // 폭 목표값과의 오차 계산 (튜닝 필요)
  if (abs(CarLine_A - CarLine_A_offset) < 8) { // 각이 offset과 가까우면 보수적으로
    A_pid.SetTunings(A_consKp, A_consKi, A_consKd);
  } else { // 각이 offset과 멀면 보수적으로
    A_pid.SetTunings(A_aggKp, A_aggKi, A_aggKd);
  }
  A_pid.Compute();  // PID 제어 계산 -> A_chng_output

  // 핸들 PID 제어
  chng_sum = W_chng_output + A_chng_output;
  if (chng_sum > chng_max){
    chng_sum = chng_max;
  }
  else if (chng_sum < -(chng_max)){
    chng_sum = -(chng_max); // +가 왼쪽으로 간 상황(우회전:'-' 필요)
  }
  pid_input = potentiometerValue; // 가변저항 값: 핸들 위치가 입력값
  setpoint = N_setpoint - chng_sum; // 셋 포인트: 기본 셋포인트(N_setpoint)에서 변화시킬 값(chng_sum)을 마이너스(좌로 틀어지면 -> 우로 틀어지도록)

  // 핸들 목표값과의 오차 계산 (튜닝 완료)
  double gap = abs(setpoint - pid_input);  
  if (gap > 8) {
    // 보수적인 제어 파라미터 적용: 멀리 있을 때 공격적이면 모터 출력이 너무 커져서 진동이 발생함. 출력값이 너무 처지지 않도록 보수적인 제어 필요.
    pid.SetTunings(consKp, consKi, consKd);
  } else {
    // 공격적인 제어 파라미터 적용: 가까울 때 공격적으로 제어하여 모터 출력이 충분히 나오도록 함
    pid.SetTunings(aggKp, aggKi, aggKd);
  }
  pid.Compute();  // PID 제어 계산 -> pid_output (핸들 돌릴 모터의 값이 나옴)

  // DC 모터 회전 제어
  if (pid_output > 0) {
    analogWrite(motor_f_1, pid_output);  // 양방향 회전
    analogWrite(motor_f_2, 0);
  } else if (pid_output < 0) {
    analogWrite(motor_f_1, 0);
    analogWrite(motor_f_2, -pid_output);  // 역방향 회전
  } else {
    analogWrite(motor_f_1, 0);  // 정지
    analogWrite(motor_f_2, 0);
  }

  // 디버깅 정보 출력
/*
Serial.println("potentiometerValue: " + String(potentiometerValue));
Serial.println("W_pid_input: " + String(W_pid_input));
Serial.println("W_chng_output: " + String(W_chng_output));
Serial.println("A_chng_output: " + String(A_chng_output));
Serial.println("chng_sum: " + String(chng_sum));
Serial.println("setpoint: " + String(setpoint));
Serial.println("pid_output: " + String(pid_output));
Serial.println();
*/


  delay(50);  // 딜레이
}
